import mydsl;
import tree;

extension org::example::dsl::EditorExtensions;
extension org::example::dsl::GenOutline reexport;

extension org::openarchitectureware::util::stdlib::io;
	
viewpoints(): {
	"Features"
	};

viewpointIcon(String vpName) : 
	switch (vpName) {
		case "Features": "vp_feature.gif"
		default: "vp_default.gif"
	};
	

// -------------------------------------------------------------------------
// Features

create UIContentNode outlineTree_Features(emf::EObject model) :
	let features = model.allLocalElements().typeSelect(FeatureClause).collect(fc|fc.feature).toSet():
		setLabel(model.label()) ->
		setImage(model.image()) ->
		setContext(model)->
		children.addAll( features.createFeatureNode(model) );

create UIContentNode createFeatureNode( String feature, emf::EObject model ):
	setLabel(feature) ->
	setImage("feature.gif") ->
	setContext(null)->
	children.addAll( feature.createRefNodes(model) );
	
createRefNodes( String feature, emf::EObject model ):
	model.allLocalElements().typeSelect(FeatureClause).select(fc|fc.feature == feature).collect(fc|fc.eContainer).createRefNode();
	
create UIContentNode createRefNode( emf::EObject obj ):
	setLabel(obj.label()) ->
	setImage(obj.image()) ->
	setContext(obj);
	
